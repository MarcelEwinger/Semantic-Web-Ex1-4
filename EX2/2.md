- [] Task 1
- [x] Task 2
- [x] Task 3
- [x] Task 4
- [x] Task 5
- [x] Task 6
- [x] Task 7
- [x] Task 8
- [x] Task 9
- [x] Task 10
- [] Task 11

1. Explore the Dataset describing the Periodical Table
 

    a. find all concepts and properties used in the sets.
      - Classes (concepts)
      - Properties


    b. guess the schema of the dataset and represent it graphically. Compare your schema with 
    the one generated by Protégé. 
    - Element --> Represents a chemical element.
    - Element Propierties
      - atomicNumber: The atomic number of the element.
      - atomicWeight: The atomic weight of the element.
      - block: The block of the periodic table (e.g., s-block, p-block).
      - casRegistryID: The CAS registry ID.
      - classification: The classification (e.g., metal, nonmetal, metalloid).
      - color: The color of the element.
      - group: The group number in the periodic table.
      - name: The name of the element.
      - state: The state of the element at room temperature (solid, liquid, gas).
  

2. Find all metallic liquid elements 

    ```
   # Define a prefix for the Periodic Table ontology to simplify the query
   PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>

   # Select all variables (in this case, all details of elements that match the criteria)
   SELECT *
   WHERE {
    # Find elements that have the classification 'Metallic'
    ?element pt:classification pt:Metallic .
    
    # Also ensure that these elements have a standard state of 'liquid'
    ?element pt:standardState pt:liquid .
   }

    ```
    ```
    1. <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Hg>
    2. <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Uub>
    ```


3. Find all elements which atomic number lies between 85 and 105. Limit the dataset to the 10 
records starting from the 3rd record. Order the results by name of elements.

    ```
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>
    
    # Select specific variables (element, name, symbol, and atomic number as a string)
    SELECT ?element ?name ?symbol (STR(?atomicNumber) AS ?atomicNumberStr)
    WHERE {
    # Match elements with an atomic number
     ?element pt:atomicNumber ?atomicNumber .
    
     # Match the name of the element    
      ?element pt:name ?name .
    
      # Match the symbol of the element
      ?element pt:symbol ?symbol .

      # Filter to include only elements with atomic numbers between 85 and 105 (inclusive)
      FILTER(?atomicNumber >= 85 && ?atomicNumber <= 105)
    }
    LIMIT 10 # Limit the results to 10 elements
    OFFSET 2 # Skip the first 2 elements in the result set
    ```

    ```
    1. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Th,"thorium","Th","90"
    2. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#At,"astatine","At","85"
    3. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#No,"nobelium","No","102"
    4. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ac,"actinium","Ac","89"
    5. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Bk,"berkelium","Bk","97"
    6. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Db,"dubnium","Db","105"
    7. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cf,"californium","Cf","98"
    8. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Np,"neptunium","Np","93"
    9. http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ra,"radium","Ra","88"
    10.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Md,"mendelevium","Md","101"
    ```


4. Extend the previous query to find whether some not actinides in among the elements. 

    ```
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>

    SELECT ?element ?name ?symbol (STR(?atomicNumber) AS ?atomicNumberStr)
        WHERE {
        # Match elements with an atomic number, name, symbol, and group
        ?element pt:atomicNumber ?atomicNumber ;
           pt:name ?name ;
           pt:symbol ?symbol ;
           pt:group ?group .

         # Filter to include only elements with atomic numbers between 85 and 105 (inclusive)
         # and exclude those in the actinoid group
         FILTER(?atomicNumber >= 85 && ?atomicNumber <= 105 && ?group !=    pt:group_actinoid)
        }
    LIMIT 10
    OFFSET 2
    ```

    ```
    1.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ra,"radium","Ra","88"
    2.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Rf,"rutherfordium","Rf","104"
    3.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Rn,"radon","Rn","86"
    4.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Lr,"lawrencium","Lr","103"
    5.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Fr,"francium","Fr","87"
    ```


5. Find all elements which name starts with the letter “C”. 

    ```
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>
    # Select specific variables (element, name, symbol, and atomic number as a string)
    SELECT ?element ?name ?symbol (STR(?atomicNumber) AS ?atomicNumberStr)
    WHERE {
         # Match elements with an atomic number, name, symbol, and group
        ?element pt:atomicNumber ?atomicNumber ;
           pt:name ?name ;
           pt:symbol ?symbol ;
           pt:group ?group .
     # Filter to include only elements whose names start with 'C'
    # "i" stands for "case-insensitive" matching --> match letters regardless of whether they are uppercase or lowercase.
    FILTER (regex(?name, "^C", "i"))
    }
    ```

    ```
    1.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cl,"chlorine","Cl","17"
    2.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cu,"copper","Cu","29"
    3.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cd,"cadmium","Cd","48"
    4.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cr,"chromium","Cr","24"
    5.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cf,"californium","Cf","98"
    6.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#C,"carbon","C","6"
    7.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ce,"cerium","Ce","58"
    8.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cm,"curium","Cm","96"
    9.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Cs,"caesium","Cs","55"
    10.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Co,"cobalt","Co","27"
    11.http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ca,"calcium","Ca","20"
    ```

6. Use the periodic table ontology to determine the number of elements in actinoid and lanthanoid groups.

    ```
    # Define a prefix for the Periodic Table ontology to simplify the query
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>

    # Select the count of elements in the Actinoid and Lanthanoid groups as strings
    SELECT
     (STR(COUNT(?elements_In_Actinoid)) AS ?Elements_In_Actinoid)
     (STR(COUNT(?elements_In_Lanthanoids)) AS ?Elements_In_Lanthanoids)
     WHERE {
    
      # First pattern: Find elements in the Actinoid group
      { ?elements_In_Actinoid pt:group pt:group_actinoid . }
    
       UNION
    
       # Second pattern: Find elements in the Lanthanoid group
       { ?elements_In_Lanthanoids pt:group pt:group_lanthanoid . }
       }

     # Explanation of why UNION is used:
     # The UNION operator combines the results of two separate patterns.
     # It ensures that elements from both the Actinoid and Lanthanoid groups are considered
     # in the same query execution. Without UNION, you would have to run two separate queries
     # to count the elements in each group individually.
    ```
    ```
   | Elements_In_Actinoid | Elements_In_Lanthanoids |   
    |----------------------|-------------------------|
    | 14                   | 14                      |
    ```


7. Compute the number of neutrons for each element Neutrons=AtomicWeight – AtomicNumber. 
The latter corresponds to the number of protons. 

    ```
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>

    SELECT ?element ?name ?symbol (STR(?atomicNumber) AS ?atomicNumberStr) (STR(?atomicWeight) AS ?atomicWeightStr)
    (STR(?atomicWeight - ?atomicNumber) AS ?neutrons)  # Calculate the number of neutrons by subtracting atomic number from atomic weight
       WHERE {
       ?element pt:name ?name ;  # Selecting the name of the element
         pt:symbol ?symbol ;    # Selecting the symbol of the element
         pt:atomicNumber ?atomicNumber ;  # Selecting the atomic number of the element
         pt:atomicWeight ?atomicWeight .  # Selecting the atomic weight of the element
    }


    ```
    ```
    | element                                                          | name      | symbol | atomicNumberStr | atomicWeightStr | neutrons   |
    |------------------------------------------------------------------|-----------|--------|-----------------|-----------------|------------|
    | http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ho   | holmium   | Ho     | 67              | 164.93032       | 97.93032   |
    | http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Hs   | hassium   | Hs     | 108             | 269             | 161.0      |
    | http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ir   | iridium   | Ir     | 77              | 192.217         | 115.216995 |
    ```


8. Find a non-metallic element with the maximum atomic number.

    ```
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>  # Prefix declaration for the periodic table ontology

    SELECT ?element ?name ?symbol (STR(?atomicNumber) AS ?atomicNumberStr)  # Selecting the element, name, symbol, and converting atomic number to string
    WHERE {
    ?element pt:classification ?classification ;  # Selecting the classification of the element
       pt:name ?name ;                            # Selecting the name of the element
       pt:symbol ?symbol ;                        # Selecting the symbol of the element
       pt:atomicNumber ?atomicNumber .            # Selecting the atomic number of the element
    
      FILTER(?classification = pt:Non-metallic)     # Filtering elements with the classification "Non-metallic"
      }
      ORDER BY DESC(?atomicNumber)  # Sorting elements by atomic number in descending order
      LIMIT 1                       # Limiting the result to only one element (the one with the highest atomic number)

    ```
    ```
    1.<http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Uuo> | ununoctium | Uuo | 118
    ```

9.  Find all metallic elements which atomic number is greater than the average. 

    ```
   PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>  # Prefix declaration for the periodic table ontology

    SELECT ?element ?name ?symbol (STR(?atomicNumber) AS ?atomicNumberStr) (STR(?avgAtomicNumber) AS ?avgAtomicNumberStr)
    WHERE {
       ?element a pt:Element ;                        # Selecting elements that are instances of the class "Element"
       pt:name ?name ;                             # Selecting the name of the element
       pt:symbol ?symbol ;                         # Selecting the symbol of the element
       pt:atomicNumber ?atomicNumber ;             # Selecting the atomic number of the element
       pt:classification pt:Metallic .             # Filtering elements classified as "Metallic"
       
      {
        SELECT (AVG(?atomicNumber) AS ?avgAtomicNumber)  # Subquery to calculate the average atomic number
        WHERE {
            ?element a pt:Element ;               # Selecting elements that are instances of the class "Element"
               pt:atomicNumber ?atomicNumber .   # Selecting the atomic number of the element
        }
     }
    
       FILTER (?atomicNumber > ?avgAtomicNumber)     # Filtering elements with atomic number greater than the average atomic number
    }
 
    ```

    ```
    <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#Ac> | actinium | Ac | 89 | 59.5
    53 Entries
    ```


10.  Select all group’s numbers, which do not have names. Try both variants of negation. 

 - Option with NON EXISTS
  
    ```
    # Prefix declaration for RDF vocabulary
    PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    
    # Prefix declaration for the periodic table ontology
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>

    # Selecting the group number as a string
    SELECT (STR(?groupNumber) AS ?groupNumberStr)
    
    WHERE {
     # Selecting resources that are instances of the class "Group"
     ?group rdf:type pt:Group .
    
     # Selecting the group number of each group
     ?group pt:number ?groupNumber .
    
     # Filtering out groups that do not have a name property
     FILTER NOT EXISTS {
        ?group pt:name ?name .
      }
    }

    ```
    ```
    groupNumberStr
    13
    7
    5
    12
    4
    9
    8
    6
    3
    10
    14
    ```
 - Option with MINUS
    ```
    # Prefix declaration for RDF vocabulary
    PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    
    # Prefix declaration for the periodic table ontology
    PREFIX pt: <http://www.daml.org/2003/01/periodictable/PeriodicTable.owl#>

    # Selecting the group number as a string
    SELECT (STR(?groupNumber) AS ?groupNumberStr)
    
    WHERE {
     # Selecting resources that are instances of the class "Group"
     ?group rdf:type pt:Group .
    
     # Selecting the group number of each group
     ?group pt:number ?groupNumber .
    
     # Excluding groups that have a name property associated with them
     MINUS {
        ?group pt:name ?name .
     }
    }

    ```
    ```
    groupNumberStr
    13
    7
    5
    12
    4
    9
    8
    6
    3
    10
    14
    ```


11.  Get more information about elements from dbpedia. Use the fact that every element has a name 
in the ontology and in rdfs:label in dbpedia resource description.  
See for instance “Helium”  http://dbpedia.org/page/Helium
